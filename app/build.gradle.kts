plugins {
    // AGP 9 has built-in Kotlin support, so the standalone
    // org.jetbrains.kotlin.android plugin is gone — AGP refuses to apply it.
    // The Compose compiler plugin is still applied separately.
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.compose)
}

android {
    namespace = "ch.swhizkid.tailtrace"
    compileSdk = 37

    defaultConfig {
        applicationId = "ch.swhizkid.tailtrace"
        minSdk = 26
        targetSdk = 35
        versionCode = 23
        versionName = "0.7.0-tailtrace"
    }

    // Fixed debug keystore committed to the repo (a debug key is non-secret — its
    // password is the well-known "android") so CI and local builds sign
    // identically. Without it each CI build minted a fresh debug key and updates
    // wouldn't install over the previous one.
    signingConfigs {
        getByName("debug") {
            storeFile = rootProject.file("debug.keystore")
            storePassword = "android"
            keyAlias = "androiddebugkey"
            keyPassword = "android"
        }
    }

    buildTypes {
        release {
            isMinifyEnabled = false
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro"
            )
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlin {
        jvmToolchain(17)
    }

    buildFeatures {
        compose = true
        buildConfig = true
    }

    packaging {
        resources {
            excludes += "/META-INF/{AL2.0,LGPL2.1}"
        }
    }
}

dependencies {
    implementation(libs.androidx.core.ktx)
    implementation(libs.androidx.lifecycle.runtime.ktx)
    implementation(libs.androidx.lifecycle.runtime.compose)
    implementation(libs.androidx.lifecycle.service)
    implementation(libs.androidx.activity.compose)

    implementation(platform(libs.androidx.compose.bom))
    implementation(libs.androidx.compose.ui)
    implementation(libs.androidx.compose.ui.graphics)
    implementation(libs.androidx.compose.ui.tooling.preview)
    implementation(libs.androidx.compose.material3)
    implementation(libs.androidx.compose.material.icons.extended)

    // No Google Play Services: location comes from the framework
    // LocationManager (see data/location/LocationProvider.kt).
    implementation(libs.osmdroid.android)

    debugImplementation(libs.androidx.compose.ui.tooling)

    testImplementation(libs.junit)
}
